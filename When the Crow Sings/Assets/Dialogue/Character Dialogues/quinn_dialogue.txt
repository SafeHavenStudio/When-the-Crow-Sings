~ quinn_directory
if QuinnBaseCompleted == false:
	=> quinn_directory_2
else:
	=> quinn_beyond_base

~ quinn_directory_2
if QuinnTaskOffered == false:
	=> quinn_first_int
if QuinnTaskAccepted == false:
	=> quinn_no_task
else:
	=> quinn_directory_3

~ quinn_directory_3
if QuinnTaskCompleted == false:
	=> quinn_task_unfin
else:
	=> quinn_task_fin


~ quinn_first_int
set QuinnTaskOffered = true;
if timeOfDay == 3:
	=> quinn_night_int
Quinn: You must be Chance.
Chance: Uh-huh.
Quinn: Nice to meet you. Officially. What brings you to the <color=#5b5b1f>greenhouse</color>? 
Quinn_Excited: Here to try my turnip-carrot hybrid?
if ifBeauAndAngel == true:
	- Angel sent me.
		Chance: <color=#813408>Angel</color> said you're up late, and that you might know something about people roaming around at night.
		=> quinn_first_int_cont
	- Beau sent me.
		Chance: <color=#813408>Beau</color> said that you know everyone around here. That you'd know if things were out of the ordinary.
		=> quinn_first_int_cont
if AngelBaseCompleted == true:
		Chance: <color=#813408>Angel</color> said you're up late, and that you might know something about people roaming around at night.
		=> quinn_first_int_cont
if BeauBaseCompleted == true:
		Chance: <color=#813408>Beau</color> said that you know everyone around here. That you'd know if things were out of the ordinary.
		=> quinn_first_int_cont
else:
	Quinn: Who even invited you in here?
	=> END

~ quinn_first_int_cont
Quinn: Mmm. Did he?
Quinn_Thinking: Well, I am up late and I provide something to just about everyone in some way or another. So, I guess he sent you somewhere good. What's up?
Chance: Someone was out past curfew and banged on my window.
Quinn_Worried: Ah, yep.
Chance_Scared: Yep?
Quinn: I've noticed some <color=#a11bbe>strays</color> around lately. Never saw them banging on any windows. They look a little funny but they just sort of roam around.
Quinn_Worried: Well, as far as I know. Nobody really messes with them so they might be violent.
Chance_Scared: Huh?!?
Quinn: But I don't really know anything about them. Never knew them or anything.
Quinn_Thinking: Maybe someone who lives outside of the central hub would know about it, since they don't really get in here often.
set FaridaHasBeenHeardOf = true;
Chance_Thinking: Outside? I thought we all lived in the <color=#5b5b1f>lodgings</color> or the <color=#5b5b1f>recreational center</color>.
Quinn: Well, kind of. Not all of us. As far as I can tell, <color=#813408>Farida</color> assigned the residencies inside the gates. Some people lived here before her.
Quinn_Thinking: . . . You know, I could get you in touch with our <color=#813408>resident outsider</color>. He's lived here for a long while, so if something changed with any residents or <color=#a11bbe>the strays</color>, he would know. 
Quinn_Excited: You couuuuld lend me a hand? I could tell <color=#813408>Farida</color> to open his gate to the <color=#5b5b1f>south-west</color> for you to go in and talk to him about it.
Chance: Doing what exactly?
Quinn: I just need a hand with some seed bags. It's about time to start incubating what will sprout in spring. Problem is, the crows that have been coming in have taken a liking to the seed bags and...
Quinn_Scared: I'm just not a big fan of birds.
Quinn: The feral ones, I mean. Not the EVOs. Obviously.
- I guess.
	set QuinnTaskAccepted = true;
	set MFQuinnTag = false;
	set MFQuinnTA = true;
	set HFQuinnTA = true;
	emit 1;
	Chance: Um, I guess I can.
	Quinn: Cool. Cool cool cool... There should be a few bags down by the <color=#5b5b1f>dining tent</color>, get them and bring them in to me and I'll see what I can do for ya.
	if HasThrownBirdseed == false:
		Chance_Thinking: How would I get past the birds?
		Quinn_Thinking: Hmm.. Well they like seed, obviously... I've heard they like shiny things too. Do you have anything like that?
		Chance_Annoyed: I mean I have some bird seed, but it's my personal stash.
		Quinn: You have a personal stash of bird seed?
		Chance: Uh, yeah. I like bird watching, it helps bring them out.
		Quinn_Worried: Why would you WANT the birds to come near you?!
		Chance_Smile: I'll try throwing the bird seed to distract them from the seed bags.
	else:
		<i>Those birds seemed really into my bird seed stash earlier.. guess I'll have to waste some more to get them off those bags.</i>
	=> ending_call
- Later.
	Chance_Sad: Maybe later.
		=> ending_call

~ quinn_night_int
Chance_Scared: Christ!
Quinn: My name is <color=#813408>Quinn</color>, actually.
Chance_Sigh: What?!
Quinn_Thinking: Uhh, my name is <color=#813408>Quinn</color>?
Chance_Angry: I know that! Why are you acting like nothing is going on outside? This place is literally MADE of windows and you're gonna pretend like you can't see that thing outside.
Quinn: Uh, yeah. It's worked out so far.
Chance_Angry: Are you insane?!
Quinn_Thinking: I mean it doesn't really bother me.. If you respect the curfew and stay out of the stray's way, they kind of just leave you alone.
Chance_Sigh: Do you people just play pretend like everything is okay?
set FaridaHasBeenHeardOf = true;
Quinn: Kinda. <color=#813408>Farida</color> set up curfew, we all stay inside, it doesn't bother anyone. Win win.
Chance_Annoyed: And it never sees you in this giant GLASS HOUSE?
Quinn_Thinking: It hasn't yet. Maybe because the lights are pink? Their eyes might not be made for that wavelength, especially since they only come out at night.
Chance_Sigh: This conversation is ridiculous. I was told you might know something about that thing, and you know it's there and just ignore it. Do you know ANYTHING useful? Like how to make it go away?!
Quinn_Thinking: Uhh... Nope. To be fair, I've never really tried. But considering how strict <color=#813408>Farida</color> is about it, I dobut she's figured it out either.
Chance_Annoyed: Do you know anything that could help me figure out how to make it leave? Like, at all?
Quinn_Worried: Not off the dome.
Quinn_Thinking:... Well, I do know someone who might, though.
Chance_Annoyed: Okay??
Quinn: If you help me out, I could put you in contact. Tell <color=#813408>Farida</color> to set his door to automatic for a while.
Chance_Sigh: What could you possibly need me to do?
Quinn_Worried: I need some seed to propogate, and it's out by the <color=#5b5b1f>dining tent</color>.
Chance_Annoyed: And you can't get them?
Quinn_Scared: Uh, no. There's a stray out there, dude.
Chance_Angry: BUT YOU WANT ME TO GO GET IT?
Quinn_Thinking: Well you're the one who wants something here..
Chance_Annoyed:...
- Fine
	set QuinnTaskAccepted = true;
	set QuinnNightTask = true;
	set MFQuinnTag = false;
	set MFQuinnTA = true;
	set HFQuinnTA = true;
	emit 1;
	Chance_Sigh: Fine. This is bullshit, you know.
	Quinn_Worried: No it's not manure, it's seed bags.
	Chance_Annoyed: I am failing to see how you are so popular.
	Quinn: Yeah whatever. Just go grab them and come back, and I'll page <color=#813408>Farida</color>.
	if HasThrownBirdseed == false:
		Chance_Sigh: How am I even going to get past them?
		Quinn_Thinking: Hmm... Well they like the seed. Do you have any bird seed?
		Chance_Annoyed: No.. I've got trail mix.
		Quinn: I'm sure that'll do the trick. Just chuck some nearby and grab those seed bags!
		Chance_Sigh: Yeah, you sound chipper for the person who doesn't have to do it. Whatever, I'll try.
	else:
		<i>Those birds seemed really into my bird seed stash earlier.. guess I'll have to waste some more to get them off those bags.</i>
	=> ending_call
- No way
	Chance_Sigh: Absolutely not.
	Quinn_Worried: Well if you come back later, you're just gonna have to find a way around the crows.
	Chance_Angry: I would much rather get attacked by crows than that thing!
	Quinn_Scared: Speak for yourself.
	=> ending_call

~ quinn_no_task
Quinn_Excited: Oh, you're back. Here to try my carnop?
Quinn_Thinking: Turnott?.. I'm workshopping it.
- Guy outside.
	set MFQuinnTag = false;
	set QuinnTaskAccepted = true;
	set MFQuinnTA = true;
	set HFQuinnTA = true;
	emit 1;
	Chance: I wanted to talk to your guy who lives outside.
	Quinn: Sure sure. Just go get those seeds for me, the bags are down by the <color=#5b5b1f>dining tent</color>. I'll page <color=#813408>Farida</color> about a delivery for <color=#813408>Yule</color> and give him a heads-up.
	if HasThrownBirdseed == false:
		Chance_Thinking: How would I get past the birds?
		Quinn_Thinking: Hmm.. Well they like seed, obviously... I've heard they like shiny things too. Do you have anything like that?
		Chance_Annoyed: I mean I have some bird seed, but it's my personal stash.
		Quinn: You have a personal stash of bird seed?
		Chance: Uh, yeah. I like bird watching, it helps bring them out.
		Quinn_Worried: Why would you WANT the birds to come near you?!
		Chance_Smile: I'll try throwing the bird seed to distract them from the seed bags.
	else:
		<i>Those birds seemed really into my bird seed stash earlier.. guess I'll have to waste some more to get them off those bags.</i>
	=> ending_call
- No thanks.
	Chance: Er, no thanks.
	Quinn: Pff. Suit yourself.
	=> END

~ quinn_task_unfin
Quinn_Worried: Really dude?
Chance: What?
Quinn_Thinking: Uhhh, I totally asked you to do something.
- What was it?
	Chance_Sad: I forgot, what was it?
	Quinn: You sure are a weird kid. I just need you to get those seed bags from the crows by the <color=#5b5b1f>dining tent</color>.
	=> END
- Yeah I know.
	Chance_Annoyed: Yeah, I know. Just haven't gotten around to it yet.
	Quinn_Thinking: Alright, whatever dude. Beat of your own drum, I guess. Even if it's a slow one.
	=> END

~ quinn_task_fin
set QuinnBaseCompleted = true;
set ForestCanBeEntered = true;
set MFYuleTag = true;
set MFQuinnTC = false;
set HFQuinnBC = true;
set YuleHasBeenHeardOf = true;
Quinn_Excited: Hey there, just in time!
Quinn: I let <color=#813408>Farida</color> know I've got <color=#813408>Yule</color> bringing in some irregular deliveries, due to the season change coming up. Sooo, You should be alright just to come and go through the <color=#5b5b1f>south-west gate</color> when you buzz it now.
Quinn_Smile: About <color=#813408>the big guy</color>? Be patient. He's old and quiet. Been here a long time, not the most social. I'm sure he'll tell you what you wanna know, but he's a little quirky. Moves to his own rhythm, you know?
Chance: Uh, sure.
Quinn: I'm basically the only person he talks to other than maybe <color=#813408>Beau</color> once in a while. Or the fish.
Chance: The fish?
Quinn_Scared: The fish.
Quinn_Smile: Thanks for your help, by the way.
if QuinnNightTask == true:
	Chance_Annoyed: Yeah, like I had a choice.
else:
	Chance_Smile: Sure, no problem.
=> quinn_task_fin_cont

~ quinn_task_fin_cont
emit 1;
if day == 1:
	=> curfew_check
else:
	=> time_forward

~ curfew_check
if timeOfDay == 2:
	It's almost time for dinner, then I've got to be back at the <color=#5b5b1f>lodging</color> before curfew. Better get going.
	set CutsceneTime = true;
	=> time_forward
else:
	=> time_forward

~ time_forward
set timeOfDay += 1;
=> ending_call

~ ending_call
call SaveGameToDisk();
=> END

~ quinn_beyond_base
Should I spend time talking to <color=#813408>Quinn</color> right now?
- Sure
	=> quinn_beyond_base_directory
- Nah
	=> END

~ quinn_beyond_base_directory
if QuinnBB == 0:
	=> Q0
else:
	set QuinnExhausted = true;
	Quinn_Smile: I've loved our chats, dude.
	Quinn_Worried: But I've got work to do, with winter coming up. I need some me time.
	Quinn: You get it.
	=> ending_call

~ BBEnd
set QuinnBB += 1;
=> ending_call

~ Q0
Quinn_Smile: Hey again, little dude.
Chance_Annoyed: I'm not little.
Quinn: Sure!
- Birds
	Chance: So what's with this whole fear of bird things you've got going on?
	Quinn_Worried: Uh, they're scary? And there's a ton of them out here.
	Chance_Thinking: Why move to someplace with a ton of crows then?
	Quinn: Uh it's not like I had a choice. I got sent out here to help with food supply by C.A.N., don't get to choose where you go.
	Chance: Okay.. So why are you afraid of them? Have they attacked you or something?
	Quinn_Scared: No, they're just creepy! And they're so fast, and they can come out of nowhere..
	Chance_Smile: That's dumb.
	Chance_Thinking: What about EVO birds?
	Quinn: Nah, they don't bother me.
	Chance_Thinking: Why not?
	Quinn_Thinking:.. I guess because they're human sized, I can always see them coming. No surprise divebombs from the sky or pecking at my fingers.
	Chance_Excited: That's so stupid dude.
	Quinn_Worried: Whatever man.
- Vegetables
	Chance: How do you decide what to grow out here?
	Quinn: Mostly just what people ask, as long as it can grow in the soil out here.
	Chance_Thinking: What do people ask for?
	Quinn_Thinking: It's mostly <color=#813408>Angel</color> since he's in charge of making food for everyone. He asks for hardy vegetables like carrots, cabbage, turnips. In the summer I try to grow corn, squash, so on.
	Chance: All seems pretty standard.
	Quinn: Yep. I keep the coffee plants alive here too, and I take special requests.
	Chance_Thinking: Special requests?
	Quinn_Thinking: Oh yeah, I mean for <color=#813408>Beau</color> I-
	Quinn_Worried: ... Nevermind.
	Chance_Thinking: What?
	Quinn_Worried: Don't worry about it.
	Quinn_Smile: Some things are best left unspoken.
	Chance_Annoyed: Then don't bring it up.
=> BBEnd

~Q1
Quinn_Smile: Hello again my dude. Here for more knowledge drops?
Chance: Is that what you call these conversations?
Quinn: Uh, yeah. Duh. 
Chance_Annoyed: Right...
- What do you do in here all day
	Chance_Thinking: So what is it you do in here all day?
	Quinn: Gardening stuff.
	Chance_Annoyed: Obviously. I meant like, specifics.
	Quinn_Thinking: Ohhh, right, right.. Well, obviously I tend to the plants that are already growing. Organize all the stuff that's ready to go out. I germinate new seeds and plant them if it's the right time of season.
	Quinn_Excited: If I've got an extra specimen, I love to do some splicing.
	Chance_Thinking: Splicing?
	Quinn_Excited: Yeah dude. Two plants with a similar vascular systems, chop off a limb from one and graft on a limb from the other. If everything's just right, boom! Hybrid.
	Chance: So like, Frankenstein?
	Quinn_Thinking: Franken-what?
	Chance: He's like, a doctor that used bunch of people's limbs and organs and stuff to make one working body.
	Quinn_Worried:....
	Quinn_Excited: How have I never heard of that before? That sounds awesome!
	Chance_Thinking: You've never heard of Frankenstein before?
	Quinn: Nope. What's it from?
	Chance_Thinking: Uh, I read it in a comic but I think it was a book a long time ago.
	Quinn_Excited: Comics? Do you have any with you?
	Chance_Smile: Yeah I managed to bring a few with me on my way here.
	Chance_Excited: There's this one about this guy who is trying to win a competition to gain freedom from an evil corporation, this guy who dresses up as a bat and kicks people's asses, and monster ones like Frankenstein.
	Quinn_Worried: A guy who dresses up as a bat?!
	Chance_Thinking: Uh, yeah?
	Quinn_Worried: What kind of freaks wrote that? Why would they dress up as an EVO?
	Chance:... I think it was before that.
	Quinn_Scared: That's even worse!!!
- Boombox
	Chance_Thinking: So how do you have a working uh... music, thing?
	Quinn: Music thing?
	Chance: Yeah, the thing in the corner playing music.
	Quinn_Smile: Ahh, Bessie, my boombox.
	Chance_Thinking: Weired name for a music player.
	Quinn_Thinking: That's just the name on the bottom of it. Boombox. Cause it plays some.. booms?
	Chance_Smile: Heheh... booms...
	Chance: But where did you get it?
	Quinn: Ah, I found it when I moved here. I think someone left it behind in the residential area.
	Chance_Thinking: Isn't that place locked up?
	Quinn_Smile: Don't sweat the details, dude.
	Quinn: Anyway, I found it out there along with some CDs for it, and I was able to convince Caleb to get it to run without batteries.
	Chance_Thinking: What's a CD?
	Quinn_Thinking: Uh.. It's the thing the music is on.. It's a.. circular.. Disk? CD?.. I dunno, it said CD on the cover, and then had a name written in pen.
	Chance: That's cool I guess. 
	Quinn_Worried: It would be more cool if C.A.N. ever agreed to send me more CDs.. They care about us making the world better, think that music is a distraction. But it totally helps me get in the zone!
	Chance_Thinking: Can you make your own?
	Quinn: I would have literally no idea how to do that. I'm all about the organic stuff.
	Chance:.. Well it's cool.
	Quinn_Smile: It sure is.
=> BBEnd

~Q2
Quinn: Sup, dude?
- Your hair
- Your glasses
=> BBEnd